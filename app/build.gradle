apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

apply from: rootProject.file('dependencies_shared.gradle')

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId buildConfig.applicationId
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode buildConfig.version.code
        versionName buildConfig.version.name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en", "ja"
        vectorDrawables.useSupportLibrary = true
        // ref. https://developer.android.com/training/wearables/apps/packaging.html#migrating
        wearAppUnbundled true

        buildConfigField("String", "PACKAGE_NAME", "\"${buildConfig.applicationId}\"")
    }

    dataBinding {
        enabled true
    }

    buildTypes {
        debug {
            // ref. https://developer.android.com/studio/build/optimize-your-build.html#disable_crashlytics
            ext.alwaysUpdateBuildId = false
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('keystore/debug.keystore')
        }
    }

    packagingOptions {
        exclude 'kotlin/**'
        exclude '**/*.kotlin_metadata'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
    }

    dynamicFeatures = [":account", ":home", ":timeline"]
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

dependencies {
    implementation project(':core')

    implementation deps.androidx.recyclerview
    kapt deps.androidx.lifecycle.compiler
    implementation deps.androidx.navigation.runtime
    implementation deps.androidx.navigation.fragment

    kapt deps.dagger.compiler
    kapt deps.dagger.androidProcessor

    implementation(deps.crashlytics) {
        transitive = true
    }
    implementation deps.firebase.core

    implementation deps.chopstick.resource
    implementation(deps.chopstick.preference) {
        exclude module: 'preference'
    }
    implementation deps.ossLicenseUi

    testImplementation deps.robolectric
}

kapt {
    useBuildCache = true
}

// Must be applied after dependencies. See https://stackoverflow.com/a/38018985
apply plugin: 'com.google.gms.google-services'
