apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//apply plugin: 'io.fabric'

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId "com.droibit.looking2"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode buildConfig.version.code
        versionName buildConfig.version.name
        resConfigs "en", "ja"
        vectorDrawables.useSupportLibrary = true
        // ref. https://developer.android.com/training/wearables/apps/packaging.html#migrating
        wearAppUnbundled true
    }

    buildTypes {
        debug {
            // ref. https://developer.android.com/studio/build/optimize-your-build.html#disable_crashlytics
            ext.alwaysUpdateBuildId = false
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('keystore/debug.keystore')
        }
    }

    packagingOptions {
        exclude 'kotlin/**'
        exclude '**/*.kotlin_metadata'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        lintConfig rootProject.file('lint.xml')

        checkDependencies true
        checkTestSources true
        explainIssues false

        // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
        checkReleaseBuilds false
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

dependencies {
    implementation deps.kotlin.jdk7
    implementation deps.kotlin.coroutines

    implementation deps.androidx.core
    implementation deps.androidx.activity
    implementation deps.androidx.fragment
    implementation deps.androidx.constraintlayout
    implementation deps.androidx.recyclerview
    implementation deps.androidx.lifecycle.livedata
    implementation deps.androidx.lifecycle.viewmodel
    kapt deps.androidx.lifecycle.compiler
    implementation deps.androidx.navigation.runtime
    implementation deps.androidx.navigation.fragment

    implementation(deps.crashlytics) {
        transitive = true
    }
    implementation deps.firebase.core

    implementation deps.androidx.wear
    implementation deps.playServices.wearable
    implementation deps.wearable.support
    compileOnly deps.wearable.wearable

    implementation deps.dagger.runtime
    implementation deps.dagger.androidSupport
    kapt deps.dagger.compiler
    kapt deps.dagger.androidProcessor
    compileOnly deps.javax.inject

    implementation(deps.twitter.kit) {
        transitive = true
    }
    implementation(deps.twitter.text) {
        exclude module: 'snakeyaml'
    }
    implementation deps.okhttp.client
    implementation deps.okhttp.logging
    implementation deps.retrofit.core
    implementation deps.retrofit.converterGson
    implementation deps.gson
    implementation(deps.picasso.runtime) {
        exclude module: 'okhttp'
    }
    implementation(deps.picasso.okhttp3Downloader)

    implementation deps.timber
    implementation deps.chopstick.resource
    implementation(deps.chopstick.preference) {
        exclude module: 'preference'
    }

    debugImplementation deps.stetho

    testImplementation deps.junit
    testImplementation deps.robolectric
    testImplementation deps.assertj
    testImplementation deps.mockito
}

kapt {
    useBuildCache = true
}

// Must be applied after dependencies. See https://stackoverflow.com/a/38018985
// apply plugin: 'com.google.gms.google-services'