import com.android.build.gradle.AppPlugin
import com.android.build.gradle.LibraryPlugin
import org.jetbrains.kotlin.gradle.dsl.KotlinCompile

buildscript { scriptHandler ->
    apply from: 'repositories.gradle',
            to: scriptHandler

    ext.buildConfig = [
            applicationId: "com.droibit.looking2",
            compileSdk   : 31,
            targetSdk    : 30,
            minSdk       : 26,
            version      : [
                    'major': 1,
                    'minor': 2,
                    'patch': 0,
                    'build': 0,
            ]
    ]
    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    ext.buildConfig.version['fullName'] = "${buildConfig.version.name}.${buildConfig.version.build}"
    ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

    apply from: rootProject.file('dependencies.gradle')

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath deps.plugin.buildTools
        classpath deps.plugin.kotlin
        classpath deps.plugin.googleServices
        classpath deps.plugin.crashlytics
        classpath deps.plugin.ossLicense
        classpath deps.plugin.navSafeArgs
        classpath deps.plugin.exhaustive
        classpath deps.plugin.daggerHilt
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.14.3'
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    buildscript {
        apply from: rootProject.file('repositories.gradle')
    }

    apply plugin: 'app.cash.exhaustive'
    apply plugin: "com.diffplug.spotless"

    plugins.whenPluginAdded { plugin ->
        if (plugin instanceof AppPlugin || plugin instanceof LibraryPlugin) {
            android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                lintOptions {
                    textReport true
                    textOutput 'stdout'
                    lintConfig rootProject.file('lint.xml')

                    checkDependencies true
                    checkTestSources true
                    explainIssues false

                    // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
                    checkReleaseBuilds false
                }
            }

            spotless {
                kotlin {
                    target("**/*.kt")
                    targetExclude("$buildDir/**/*.kt")
                    targetExclude("**/generated/**/*.kt")
                    ktlint(versions.ktlint)
                            .userData([
                                    "android"     : "true",
                                    "experimental": "true"
                            ])
                }
            }
        }

        configurations.all {
            exclude group: 'androidx.preference', module: 'preference'
            exclude group: 'androidx.appcompat', module: 'appcompat'
        }
    }

    tasks.withType(KotlinCompile).configureEach { task ->
        task.kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += [
                    '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
                    '-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi',
                    '-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi',
                    '-Xuse-experimental=kotlin.time.ExperimentalTime',
                    '-Xinline-classes'
            ]
        }
    }
}